<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark" title="{data.label} Recent VODs" viewActivate="init(event)">
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayList;
			import mx.events.FlexEvent;
			import mx.events.PropertyChangeEvent;
			import mx.rpc.events.ResultEvent;
			
			import spark.events.IndexChangeEvent;
			import spark.events.ViewNavigatorEvent;
			
			public var startIndex:int;
			public var loading:Boolean;
			public var months:Object = new Object();
			private var searchTerm:String;
			
			protected function init(event:ViewNavigatorEvent):void
			{
				months["01"] = "January";
				months["02"] = "February";
				months["03"] = "March";
				months["04"] = "April";
				months["05"] = "May";
				months["06"] = "June";
				months["07"] = "July";
				months["08"] = "August";
				months["09"] = "September";
				months["10"] = "October";
				months["11"] = "November";
				months["12"] = "December";
				
				searchTerm = data.name;
				myBusy.visible = true;
				startIndex = 1;
				channelUploads.url = "https://gdata.youtube.com/feeds/api/videos?q="+searchTerm+"&orderby=published&max-results=20&v=2&start-index="+startIndex;
				channelUploads.send();
			}
			
			protected function playVideo(event:IndexChangeEvent):void
			{
				var myObject:Object = new Object();
				myObject.title = videosList.selectedItem.title;
				myObject.url = videosList.selectedItem.group.videoid;
				navigator.pushView(VideoDetails, myObject);
			}

			
			protected function channelUploads_resultHandler(event:ResultEvent):void
			{
				if(videosList.dataProvider == null){
					videosList.dataProvider = event.result.feed.entry;
				} else {
					for each ( var item : * in event.result.feed.entry){
						videosList.dataProvider.addItem(item);
					}
				}
				
				loading = false;
				myBusy.visible = false;
			}
			
			protected function videosList_creationCompleteHandler(event:FlexEvent):void
			{
				videosList.scroller.viewport.addEventListener(PropertyChangeEvent.PROPERTY_CHANGE, propertyChangeHandler);
			}
			
			protected function propertyChangeHandler( event : PropertyChangeEvent):void {
				if(event.property == "verticalScrollPosition"){
					if(event.newValue == ( event.currentTarget.measuredHeight - event.currentTarget.height)){
						loadMore();
					}
				}
			}
			
			public function loadMore():void{
				if(!loading){
					myBusy.visible = true;
					loading = true;
					startIndex+=20;
					channelUploads.url = "https://gdata.youtube.com/feeds/api/videos?q="+searchTerm+"&orderby=published&max-results=20&v=2&start-index="+startIndex;
					channelUploads.send();
				}
			}
			
			protected function customSearch():void
			{
				if(searchTxt.text == ""){
					//do nothing
				} else {
					
					stage.focus = null;
					
					searchTerm = searchTxt.text;
					
					myBusy.visible = true;
					startIndex = 1;
					
					videosList.dataProvider = new ArrayList(new Array());
					
					channelUploads.url = "https://gdata.youtube.com/feeds/api/videos?q="+searchTerm+"&orderby=published&max-results=20&v=2&start-index="+startIndex;
					channelUploads.send();
				}
			}
			
		]]>
	</fx:Script>
	
	<fx:Declarations>
		<s:HTTPService id="channelUploads" result="channelUploads_resultHandler(event)"/>
	</fx:Declarations>
	
	<s:navigationContent>
		<s:Button click="navigator.popView()">
			<s:icon>
				<s:MultiDPIBitmapSource source160dpi="@Embed('assets/icons/160/115-Film.png')"
										source240dpi="@Embed('assets/icons/240/115-Film.png')"
										source320dpi="@Embed('assets/icons/320/115-Film@2x.png')"/>
			</s:icon>
		</s:Button>
	</s:navigationContent>
	
	<s:Rect x="0" width="100%" height="55" top="0">		
		<s:fill>
			<s:LinearGradient rotation="90">
				<s:entries>
					<s:GradientEntry color="#006D7B"/>
					<s:GradientEntry color="#1F3C4B"/>														
				</s:entries>
			</s:LinearGradient>	
		</s:fill>
	</s:Rect>
	
	<s:TextInput id="searchTxt" left="10" right="55" top="10" height="35" prompt="Type Search Term" autoCorrect="false" enter="customSearch()"/>
	<s:Image source="@Embed('assets/icons/search.png')" smooth="true" scaleMode="letterbox" width="35" height="35" right="10" y="10" click="customSearch()"/>
	
	
	<s:List styleName="myListStyle" creationComplete="videosList_creationCompleteHandler(event)" id="videosList" change="playVideo(event)" width="100%" top="55" bottom="0">
		<s:itemRenderer>
			<fx:Component>
				<s:IconItemRenderer messageStyleName="myMessageStyle2" iconWidth="67" iconHeight="50" iconFunction="myIconFunction" messageFunction="myMessageFunction" labelFunction="myLabelFunction">
					<fx:Script>
						<![CDATA[
							
							public function myIconFunction(item:Object):String{		
								return "http://i1.ytimg.com/vi/"+data.group.videoid+"/default.jpg";								
							}
							
							public function myLabelFunction(item:Object):String{								
								return data.title;
							}
							
							public function myMessageFunction(item:Object):String{								
								return data.author.name+ "\n" + outerDocument.months[data.published.slice(5,7)]+" "+data.published.slice(8,10)+", "+data.published.slice(0,4);
							}
							
						]]>
					</fx:Script>
				</s:IconItemRenderer>
			</fx:Component>
		</s:itemRenderer>
	</s:List>
	
	<s:BusyIndicator id="myBusy" verticalCenter="0" horizontalCenter="0" visible="false"/>
	
</s:View>
