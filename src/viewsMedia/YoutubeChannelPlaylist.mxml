<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark" tabBarVisible="false" title="{data.title}" viewActivate="init(event)">
	<fx:Script>
		<![CDATA[
			import mx.collections.ArrayList;
			import mx.events.FlexEvent;
			import mx.events.PropertyChangeEvent;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			import spark.events.IndexChangeEvent;
			import spark.events.ViewNavigatorEvent;
			
			private var pageToken:String;
			private var loading:Boolean;
			public var months:Object = new Object();
			
			protected function init(event:ViewNavigatorEvent):void
			{
				months["01"] = "January";
				months["02"] = "February";
				months["03"] = "March";
				months["04"] = "April";
				months["05"] = "May";
				months["06"] = "June";
				months["07"] = "July";
				months["08"] = "August";
				months["09"] = "September";
				months["10"] = "October";
				months["11"] = "November";
				months["12"] = "December";
				
				myBusy.visible = true;
				pageToken = "";
				playlistService.url = "https://www.googleapis.com/youtube/v3/playlistItems?playlistId="+data.id+"&maxResults=50&part=snippet&key="+EsportsApp.YOUTUBE_API_KEY;
				playlistService.send();
			}
			
			protected function videosList_creationCompleteHandler(event:FlexEvent):void
			{
				videosList.scroller.viewport.addEventListener(PropertyChangeEvent.PROPERTY_CHANGE, propertyChangeHandler);
			}
			
			protected function videosList_changeHandler(event:IndexChangeEvent):void
			{
				var myObject:Object = new Object();
				myObject.title = videosList.selectedItem.snippet.title;
				myObject.url = videosList.selectedItem.snippet.resourceId.videoId;
				navigator.pushView(VideoDetails, myObject);
			}
			
			protected function playlistService_resultHandler(event:ResultEvent):void
			{
				var rawData:Object = JSON.parse(String(event.result));
				pageToken = rawData.nextPageToken;
				
				if(videosList.dataProvider == null){
					var videosArray:ArrayList = new ArrayList(rawData.items as Array);
					videosList.dataProvider = videosArray;
				} else {
					for each (var item:* in rawData.items){
						videosList.dataProvider.addItem(item);
					}
				}
				
				loading = false;
				myBusy.visible = false;
			}
			

			protected function propertyChangeHandler( event : PropertyChangeEvent):void {
				if(event.property == "verticalScrollPosition"){
					if(event.newValue == ( event.currentTarget.measuredHeight - event.currentTarget.height)){
						loadMore();
					}
				}
			}
			
			public function loadMore():void{
				if(!loading){
					myBusy.visible = true;
					loading = true;
					playlistService.url = "https://www.googleapis.com/youtube/v3/playlistItems?playlistId="+data.id+"&pageToken="+pageToken+"&maxResults=50&part=snippet&key="+EsportsApp.YOUTUBE_API_KEY;
					playlistService.send();
				}
			}
			
			protected function playlistService_faultHandler(event:FaultEvent):void
			{
				loading = false;
				myBusy.visible = false;
			}
			
		]]>
	</fx:Script>
	<fx:Declarations>
		<s:HTTPService id="playlistService" result="playlistService_resultHandler(event)" resultFormat="text" fault="playlistService_faultHandler(event)"/>
	</fx:Declarations>
	
	<s:List id="videosList" creationComplete="videosList_creationCompleteHandler(event)" change="videosList_changeHandler(event)" width="100%" height="100%">
		<s:itemRenderer>
			<fx:Component>
				<s:IconItemRenderer messageStyleName="myMessageStyle" iconWidth="67" iconHeight="50" iconScaleMode="letterbox" iconFunction="myIconFunction" messageFunction="myMessageFunction" labelFunction="myLabelFunction">
					<fx:Script>
						<![CDATA[
							
							public function myIconFunction(item:Object):String{		
								return data.snippet.thumbnails.medium.url;
							}
							
							public function myLabelFunction(item:Object):String{								
								return data.snippet.title;
							}
							
							public function myMessageFunction(item:Object):String{								
								return String(data.snippet.publishedAt).substr(0, 10);
							}
							
						]]>
					</fx:Script>
				</s:IconItemRenderer>
			</fx:Component>
		</s:itemRenderer>
	</s:List>
	
	<s:BusyIndicator id="myBusy" verticalCenter="0" horizontalCenter="0" visible="false"/>
	
	<s:navigationContent>
		<s:Button click="navigator.popView()">
			<s:icon>
				<s:MultiDPIBitmapSource source160dpi="@Embed('assets/icons/160/115-Film.png')"
										source240dpi="@Embed('assets/icons/240/115-Film.png')"
										source320dpi="@Embed('assets/icons/320/115-Film@2x.png')"/>
			</s:icon>
		</s:Button>
	</s:navigationContent>	
	
</s:View>
